package org.drools.planner.examples.tournaments;

import org.drools.planner.core.score.buildin.hardandsoft.HardAndSoftScoreCalculator;
import org.drools.planner.core.score.constraint.IntConstraintOccurrence;
import org.drools.planner.core.score.constraint.ConstraintType;

import org.drools.planner.examples.tournaments.model.Configuration;
import org.drools.planner.examples.tournaments.model.Match;
import org.drools.planner.examples.tournaments.model.PauseMatch;
import org.drools.planner.examples.tournaments.model.TeamsMatch;
import org.drools.planner.examples.tournaments.model.Team;
import org.drools.planner.examples.tournaments.model.Slot;

import java.util.Arrays;
import java.util.Collection;

global HardAndSoftScoreCalculator scoreCalculator;

rule "Find out how many minimal distances between matches have been broken"
    when
		$c: Configuration()
		$s1: Slot($m1: match)
		$s2: Slot(this != $s1, $c.isMinimalDistanceBroken($s1, $s2), $m2: match)
		TeamsMatch(this == $m1, this.areTeamsShared($m2))
    then
        insertLogical(
			new IntConstraintOccurrence("minimumDistanceBroken", ConstraintType.NEGATIVE_HARD,
                1, $m1, $s1, $s2)        
        );
end

rule "Find out how many slots share a single match"
    when
        $s1: Slot($m: match)
        accumulate(
            $slot: Slot(this != $s1, $m == match),
            $count: count($slot)
        )
    then
        insertLogical(
			new IntConstraintOccurrence("singleMatchShared", ConstraintType.NEGATIVE_HARD,
                $count.intValue() * 10, $m)        
        );
end

rule "Get overhead per team"
    when
        $c: Configuration()
        $t: Team()
        accumulate(
            Slot($s: this, this.plays($t)),
            $min: min($s.getNumber()),
            $max: max($s.getNumber()),
            $count: count($s)
        )
    then
        insertLogical(
            new IntConstraintOccurrence("teamOverhead", ConstraintType.NEGATIVE_SOFT,
                $count.intValue() < 2 ? 0 : 
                    Math.max(0, $max.intValue() - $min.intValue() + $c.getSlotsPerMatch() - $c.getSlotsPerMatch() * $count.intValue()), $t)
        );
end

rule "Hard score means how many teams have matches directly adjacent"
	salience -1
    when
        $total : Number () from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_HARD, $weight : weight),
            sum($weight)
        )
    then
        scoreCalculator.setHardConstraintsBroken($total.intValue());
end

rule "Soft score is a sum of all the overheads"
	salience -1
    when
        $total : Number () from accumulate(
            IntConstraintOccurrence(constraintType == ConstraintType.NEGATIVE_SOFT, $weight: weight),
            sum($weight)
        )
    then
        scoreCalculator.setSoftConstraintsBroken($total.intValue());
end
